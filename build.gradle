description = 'Sentry client and appenders for diverse logging frameworks'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'checkstyle'

    group = 'net.kencochrane.raven'
    version = '4.1.2-SNAPSHOT'

    ext.slf4jVersion = '1.7.5'
    ext.guavaVersion = '15.0'
    ext.jacksonVersion = '2.2.3'
    ext.servletVersion = '3.0.1'
    ext.logbackVersion = '1.0.13'
    ext.mockitoVersion = '1.9.5'
    ext.jmockitVersion = '1.5'
    ext.testngVersion = '6.8.7'
    ext.hamcrestVersion = '1.3'

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        sourceSets = [sourceSets.main]
    }

    repositories {
        mavenCentral()
    }

    test {
        useTestNG()
    }

    sourceSets {
        integrationTest {
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')
        }
    }

    task integrationTest(type: Test) {
        description = 'Runs the integration tests.'
        group = 'verification'

        useTestNG()
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        reports.html.destination = file("$buildDir/reports/integration-tests")
    }

    dependencies {
        integrationTestCompile sourceSets.main.output
        integrationTestCompile configurations.testCompile
        integrationTestRuntime configurations.testRuntime
    }
}
