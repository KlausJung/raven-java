description = 'Sentry client written in Java'

configurations {
    provided
    compile.extendsFrom provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: ext.slf4jVersion
    compile group: 'com.google.guava', name: 'guava', version: ext.guavaVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: ext.jacksonVersion
    provided group: 'javax.servlet', name: 'javax.servlet-api', version: ext.servletVersion
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: ext.jacksonVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: ext.mockitoVersion
    testCompile group: 'com.googlecode.jmockit', name: 'jmockit', version: ext.jmockitVersion
    testCompile group: 'org.testng', name: 'testng', version: ext.testngVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: ext.hamcrestVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: ext.hamcrestVersion
    integrationTestRuntime group: 'org.slf4j', name: 'slf4j-jdk14', version: ext.slf4jVersion
}

task generateBuildNumber(type: Copy) {
    description = 'Creates a raven-build.properties with the current build number'

    from 'src/main/resources/raven-build.properties'
    into "$buildDir/generated-resources"
    expand([
            buildNumber: 'git describe --tags'.execute(Collections.emptyList(), rootDir).text.trim()
    ])
}

jar {
    from "$buildDir/generated-resources/raven-build.properties"
}

jar.dependsOn generateBuildNumber
